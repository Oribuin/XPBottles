import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "java-library"
    id "com.github.johnrengelman.shadow" version "7.1.0"
    id "maven-publish"
}

group "xyz.oribuin"
version "2.0"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.encoding = "UTF-8"
}

repositories {
    mavenCentral()
    mavenLocal()

    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    maven { url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.20.1-R0.1-SNAPSHOT"
    compileOnly "org.jetbrains:annotations:23.0.0"
}

shadowJar {
    archiveClassifier.set(null)
}
processResources {
    from(sourceSets.main.resources.srcDirs) {
        include "**/*.yml"
        filter ReplaceTokens, tokens: ["version": project.property("version")]
        duplicatesStrategy DuplicatesStrategy.INCLUDE
    }
}

// Jar Publishing
publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifactId = "xpbottles"
            pom {
                name = "xpbottles"
            }
        }
    }

    repositories {
        if (project.hasProperty("mavenUser") && project.hasProperty("mavenPassword")) {
            maven {
                credentials {
                    username project.mavenUser
                    password project.mavenPassword
                }

                def releasesRepoUrl = "https://repo.rosewooddev.io/repository/public-releases/"
                def snapshotsRepoUrl = "https://repo.rosewooddev.io/repository/public-snapshots/"
                url = version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }
}

build.dependsOn shadowJar